plugins {
    id 'java'
    id 'maven-publish'
    id 'org.jreleaser' version '1.18.0'
}

group = 'ru.ilug'
version = '1.0.0'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'it.unimi.dsi:fastutil:8.5.15'

    compileOnly("org.projectlombok:lombok:1.18.38")
    annotationProcessor("org.projectlombok:lombok:1.18.38")

    testCompileOnly("org.projectlombok:lombok:1.18.38")
    testAnnotationProcessor("org.projectlombok:lombok:1.18.38")
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

test {
    useJUnitPlatform()
}

java {
    withJavadocJar()
    withSourcesJar()
}

description = 'Simple byte mapper for Java objects'

publishing {
    publications {
        release(MavenPublication) {
            from(components["java"])

            groupId = project.group
            artifactId = project.name

            pom {
                name.set(project.name)
                description.set(project.description)
                url.set("https://github.com/FalmerF/byte-mapper")
                issueManagement {
                    url.set("https://github.com/FalmerF/byte-mapper/issues")
                }

                scm {
                    url.set("https://github.com/FalmerF/byte-mapper")
                    connection.set("scm:git://github.com/FalmerF/byte-mapper.git")
                    developerConnection.set("scm:git://github.com/FalmerF/byte-mapper.git")
                }

                licenses {
                    license {
                        name.set("The Apache Software License, Version 2.0")
                        url.set("http://www.apache.org/licenses/LICENSE-2.0.txt")
                        distribution.set("repo")
                    }
                }

                developers {
                    developer {
                        id.set("FalmerF")
                        name.set("Ivan Lugovoy")
                        email.set("foulorown@gmail.com")
                        url.set("https://ilug.ru")
                    }
                }
            }
        }
    }
    repositories {
        maven {
            setUrl(layout.buildDirectory.dir("staging-deploy"))
        }
    }
}

jreleaser {
    project {
        inceptionYear = "2025"
        author("@FalmerF")
    }
    signing {
        active = 'ALWAYS'
        armored = true
        verify = true
        mode = 'FILE'
    }
    release {
        github {
            skipRelease = true
            skipTag = true
            sign = true
            branch = "main"
            branchPush = "main"
            overwrite = true
        }
    }
    deploy {
        maven {
            mavenCentral.create("sonatype") {
                active = 'ALWAYS'
                url = "https://central.sonatype.com/api/v1/publisher"
                stagingRepository(layout.buildDirectory.dir("staging-deploy").get().toString())
                setAuthorization("Basic")
                retryDelay = 60
            }
        }
    }
}